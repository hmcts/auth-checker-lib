plugins {
    id 'java-library'
    id 'maven-publish'
    id "io.freefair.lombok" version "8.3"
}

group = 'uk.gov.hmcts.reform.auth'
version = '2.1.5'

sourceCompatibility = 11
targetCompatibility = 11
tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}


repositories {
    mavenLocal()
    mavenCentral()
}


dependencies {

    api group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version:'2.15.2'
    api group: 'org.apache.httpcomponents', name: 'httpclient', version:'4.5.14'
    api group: 'io.jsonwebtoken', name: 'jjwt', version:'0.9.1'
    api group: 'com.google.guava', name: 'guava', version:'32.1.2-jre'
    api group: 'org.springframework.boot', name: 'spring-boot-autoconfigure', version:'2.7.15'
    api group: 'org.springframework.boot', name: 'spring-boot-starter-security', version:'2.7.15'
    testImplementation group: 'com.github.tomakehurst', name: 'wiremock', version:'3.0.1'
    testImplementation group: 'org.assertj', name: 'assertj-core', version:'3.24.2'
    testImplementation group: 'junit', name: 'junit', version:'4.13.2'
    testImplementation group: 'org.mockito', name: 'mockito-all', version:'1.10.19'
    testImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-web', version:'2.7.15'
    testImplementation(group: 'org.springframework.boot', name: 'spring-boot-starter-test', version:'2.7.15') {
        exclude(module: 'commons-logging')
    }
    compileOnly group: 'javax.servlet', name: 'javax.servlet-api', version:'4.0.1'
    compileOnly group: 'com.google.code.findbugs', name: 'annotations', version: '3.0.1'

}


jar {
    manifest {
        attributes 'Implementation-Title': project.name, 'Implementation-Version': project.version
    }
}

task printVersion {
    doLast {
        print project.version
    }
}

def pomConfig = {
    licenses {
        license {
            name "MIT License"
            url "http://www.opensource.org/licenses/mit-license.php"
            distribution "repo"
        }
    }

    scm {
        url "https://github.com/hmcts/auth-checker-lib"
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

publishing {
    publications {
        Main(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            groupId project.group
            artifactId 'auth-checker-lib'
            version project.version

            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'Client library for communicating with IDAM')
                root.appendNode('name', 'IDAM Auth Checker Libary')
                root.appendNode('url', 'https://github.com/hmcts/auth-checker-lib')
                root.children().last() + pomConfig
            }
        }
    }
}

