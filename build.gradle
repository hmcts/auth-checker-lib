plugins {
    id 'java-library'
    id 'maven-publish'
    id "io.freefair.lombok" version "8.6"
}

def buildNumber = System.getenv("RELEASE_VERSION")?.replace("refs/tags/", "") ?: "DEV-SNAPSHOT"

group = 'uk.gov.hmcts.reform.auth'
version = buildNumber

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    api group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.17.1'
    api group: 'org.apache.httpcomponents.client5', name: 'httpclient5', version: '5.3.1'
    api group: 'io.jsonwebtoken', name: 'jjwt-api', version: '0.12.5'
    api group: 'com.google.guava', name: 'guava', version: '33.2.1-jre'
    api group: 'org.springframework.boot', name: 'spring-boot-autoconfigure', version: '3.3.1'
    api group: 'org.springframework.boot', name: 'spring-boot-starter-security', version: '3.3.1'
    api group: 'org.springframework.boot', name: 'spring-boot-starter-web', version: '3.3.1'
    testImplementation group: 'org.wiremock', name: 'wiremock', version: '3.7.0'
    testImplementation group: 'org.assertj', name: 'assertj-core', version: '3.26.0'
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter', version: '5.10.2'
    testImplementation group: 'org.hamcrest', name: 'hamcrest', version: '2.2'
    testImplementation group: 'org.mockito', name: 'mockito-core', version: '5.12.0'
    testImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-web', version: '3.3.1'
    testImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-test', version: '3.3.1'
    compileOnly group: 'jakarta.servlet', name: 'jakarta.servlet-api', version: '6.1.0'
    compileOnly group: 'com.github.spotbugs', name: 'spotbugs-annotations', version: '4.8.6'
}

test {
    useJUnitPlatform()
}

jar {
    manifest {
        attributes 'Implementation-Title': project.name, 'Implementation-Version': project.version
    }
}

task printVersion {
    doLast {
        print project.version
    }
}

def pomConfig = {
    licenses {
        license {
            name "MIT License"
            url "http://www.opensource.org/licenses/mit-license.php"
            distribution "repo"
        }
    }

    scm {
        url "https://github.com/hmcts/auth-checker-lib"
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

publishing {
    publications {
        Main(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            groupId project.group
            artifactId 'auth-checker-lib'
            version project.version

            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'Client library for communicating with IDAM')
                root.appendNode('name', 'IDAM Auth Checker Libary')
                root.appendNode('url', 'https://github.com/hmcts/auth-checker-lib')
                root.children().last() + pomConfig
            }
        }
    }
}

