plugins {
    id 'java'
    id 'maven'
    id 'maven-publish'
    id 'java-library'
    id "com.jfrog.bintray" version "1.7.3"
}

group = 'uk.gov.hmcts.reform.auth'
version = '2.1.4'

sourceCompatibility = 1.8
targetCompatibility = 1.8
tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}


repositories {
    maven { url "http://dl.bintray.com/hmcts/hmcts-maven/" }
    mavenLocal()
    jcenter()
}


dependencies {

    compile group: 'uk.gov.hmcts.reform', name: 'java-logging-spring', version:'5.0.1'
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version:'2.9.9.1'
    compile group: 'org.apache.httpcomponents', name: 'httpclient', version:'4.5.9'
    compile group: 'io.jsonwebtoken', name: 'jjwt', version:'0.9.1'
    compile group: 'com.google.guava', name: 'guava', version:'28.0-jre'
    compile group: 'org.springframework.boot', name: 'spring-boot-autoconfigure', version:'2.1.6.RELEASE'
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-security', version:'2.1.6.RELEASE'
    compile group: 'org.projectlombok', name: 'lombok', version:'1.18.8'
    testCompile group: 'com.github.tomakehurst', name: 'wiremock', version:'2.23.2'
    testCompile group: 'org.assertj', name: 'assertj-core', version:'3.12.2'
    testCompile group: 'junit', name: 'junit', version:'4.12'
    testCompile group: 'org.mockito', name: 'mockito-all', version:'1.10.19'
    testCompile group: 'org.springframework.boot', name: 'spring-boot-starter-web', version:'2.1.6.RELEASE'
    testCompile(group: 'org.springframework.boot', name: 'spring-boot-starter-test', version:'2.1.6.RELEASE') {
        exclude(module: 'commons-logging')
    }
    compileOnly group: 'javax.servlet', name: 'javax.servlet-api', version:'4.0.1'
    compileOnly group: 'com.google.code.findbugs', name: 'annotations', version: '3.0.1'

}


jar {
    manifest {
        attributes 'Implementation-Title': project.name, 'Implementation-Version': project.version
    }
}

task printVersion {
    doLast {
        print project.version
    }
}

def pomConfig = {
    licenses {
        license {
            name "MIT License"
            url "http://www.opensource.org/licenses/mit-license.php"
            distribution "repo"
        }
    }

    scm {
        url "https://github.com/hmcts/auth-checker-lib"
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

publishing {
    publications {
        Main(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            groupId project.group
            artifactId 'auth-checker-lib'
            version project.version

            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'Client library for communicating with IDAM')
                root.appendNode('name', 'IDAM Auth Checker Libary')
                root.appendNode('url', 'https://github.com/hmcts/auth-checker-lib')
                root.children().last() + pomConfig
            }
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publications = ['Main']
    publish = true
    pkg {
        repo = 'hmcts-maven'
        name = 'auth-checker-lib'
        userOrg = 'hmcts'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/hmcts/auth-checker-lib'
        version {
            name = project.version
        }
    }
}
