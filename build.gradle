plugins {
    id 'java-library'
    id 'maven-publish'
    id 'io.freefair.lombok' version '9.1.0'
    id 'io.spring.dependency-management' version '1.1.7'
}

def buildNumber = System.getenv("RELEASE_VERSION") ?: "DEV-SNAPSHOT"

group = 'com.github.hmcts'
version = buildNumber

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

compileJava {
    options.compilerArgs << '-parameters'
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

repositories {
    mavenLocal()
    mavenCentral()
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.boot:spring-boot-dependencies:3.5.0"
    }
}

dependencies {
    api 'org.springframework.boot:spring-boot-starter-security'
    api 'org.springframework.boot:spring-boot-starter-web'
    api 'org.apache.httpcomponents.client5:httpclient5:5.5.1'
    api 'io.jsonwebtoken:jjwt-api:0.13.0'
    api 'com.google.guava:guava:33.5.0-jre'
    testImplementation 'org.wiremock:wiremock-standalone:3.13.2'
    compileOnly 'com.github.spotbugs:spotbugs-annotations:4.9.8'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

test {
    useJUnitPlatform()
}

jar {
    manifest {
        attributes(
                'Implementation-Title': project.name,
                'Implementation-Version': project.version
        )
    }
}

tasks.register('printVersion') {
    doLast {
        print project.version
    }
}

def pomConfig = {
    licenses {
        license {
            name "MIT License"
            url "http://www.opensource.org/licenses/mit-license.php"
            distribution "repo"
        }
    }

    scm {
        url "https://github.com/hmcts/auth-checker-lib"
    }
}

tasks.register('sourcesJar', Jar) {
    dependsOn classes
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

tasks.register('javadocJar', Jar) {
    dependsOn javadoc
    archiveClassifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

publishing {
    publications {
        Main(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            groupId = project.group
            artifactId = 'auth-checker-lib'
            version = project.version

            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'Client library for communicating with IDAM')
                root.appendNode('name', 'IDAM Auth Checker Libary')
                root.appendNode('url', 'https://github.com/hmcts/auth-checker-lib')
                root.children().last() + pomConfig
            }
        }
    }

    repositories {
        maven {
            name = "AzureArtifacts"
            url = uri("https://pkgs.dev.azure.com/hmcts/Artifacts/_packaging/hmcts-lib/maven/v1")
            credentials {
                username = System.getenv("AZURE_DEVOPS_ARTIFACT_USERNAME")
                password = System.getenv("AZURE_DEVOPS_ARTIFACT_TOKEN")
            }
        }
    }
}
