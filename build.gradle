plugins {
    id 'java-library'
    id 'maven-publish'
    id 'io.freefair.lombok' version '8.13.1'
}

def buildNumber = System.getenv("RELEASE_VERSION")?: "DEV-SNAPSHOT"

group = 'com.github.hmcts'
version = buildNumber

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

compileJava {
    options.compilerArgs << '-parameters'
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

repositories {
    mavenLocal()
    mavenCentral()
}

dependencies {
    api group: 'org.apache.httpcomponents.client5', name: 'httpclient5', version: '5.5'
    api group: 'io.jsonwebtoken', name: 'jjwt-api', version: '0.12.6'
    api group: 'com.google.guava', name: 'guava', version: '33.4.8-jre'
    api group: 'org.springframework.boot', name: 'spring-boot-starter-security', version: '3.5.0'
    api group: 'org.springframework.boot', name: 'spring-boot-starter-web', version: '3.5.0'

    testImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-test', version: '3.5.0'
    testImplementation group: 'org.wiremock', name: 'wiremock', version: '3.13.1'

    compileOnly group: 'com.github.spotbugs', name: 'spotbugs-annotations', version: '4.9.3'
}

test {
    useJUnitPlatform()
}

jar {
    manifest {
        attributes 'Implementation-Title': project.name, 'Implementation-Version': project.version
    }
}

tasks.register('printVersion') {
    doLast {
        print project.version
    }
}

def pomConfig = {
    licenses {
        license {
            name "MIT License"
            url "http://www.opensource.org/licenses/mit-license.php"
            distribution "repo"
        }
    }

    scm {
        url "https://github.com/hmcts/auth-checker-lib"
    }
}

tasks.register('sourcesJar', Jar) {
    dependsOn classes
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

tasks.register('javadocJar', Jar) {
    dependsOn javadoc
    archiveClassifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

publishing {
    publications {
        Main(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            groupId = project.group
            artifactId = 'auth-checker-lib'
            version = project.version

            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'Client library for communicating with IDAM')
                root.appendNode('name', 'IDAM Auth Checker Libary')
                root.appendNode('url', 'https://github.com/hmcts/auth-checker-lib')
                root.children().last() + pomConfig
            }
        }
    }

    repositories {
        maven {
            name = "AzureArtifacts"
            url = uri("https://pkgs.dev.azure.com/hmcts/Artifacts/_packaging/hmcts-lib/maven/v1")
            credentials {
                username = System.getenv("AZURE_DEVOPS_ARTIFACT_USERNAME")
                password = System.getenv("AZURE_DEVOPS_ARTIFACT_TOKEN")
            }
        }
    }
}
